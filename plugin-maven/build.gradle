import com.github.mustachejava.DefaultMustacheFactory

import java.nio.file.Files
import java.nio.file.Paths

import static java.nio.charset.StandardCharsets.UTF_8
import static java.nio.file.StandardOpenOption.CREATE_NEW
import static java.nio.file.StandardOpenOption.TRUNCATE_EXISTING

ext.artifactId = project.artifactIdMaven
ext.version = project.versionMaven
apply from: rootProject.file("gradle/java-setup.gradle")
apply from: rootProject.file("gradle/java-publish.gradle")

final PROJECT_DIR = project.projectDir.toString()
final BUILD_DIR = project.buildDir.toString()
final MAVEN_PROJECT_DIR = "${BUILD_DIR}/mavenProject"
final LOCAL_MAVEN_REPO_DIR = "${BUILD_DIR}/localMavenRepository"

final SPOTLESS_LIB_VERSION = "1.0.0-SNAPSHOT" // should be project.stableLib once maven plugin stabilizes

buildscript {
	repositories { mavenCentral() }
	dependencies { classpath "com.github.spullara.mustache.java:compiler:${VER_MUSTACHE}" }
}

dependencies {
	compile project(":lib")
	compile project(":lib-extra")
	compileOnly "org.apache.maven:maven-plugin-api:${VER_MAVEN_API}"
	compileOnly "org.apache.maven.plugin-tools:maven-plugin-annotations:${VER_MAVEN_API}"
	compileOnly "org.eclipse.aether:aether-api:${VER_ECLIPSE_AETHER}"
	compileOnly "org.eclipse.aether:aether-util:${VER_ECLIPSE_AETHER}"

	testCompile project(":testlib")
	testCompile "junit:junit:${VER_JUNIT}"
	testCompile "org.assertj:assertj-core:${VER_ASSERTJ}"
	testCompile "org.mockito:mockito-core:${VER_MOCKITO}"
	testCompile "com.diffplug.durian:durian-testlib:${VER_DURIAN}"
	testCompile "com.github.spullara.mustache.java:compiler:${VER_MUSTACHE}"
	testCompile "org.apache.maven:maven-plugin-api:${VER_MAVEN_API}"
	testCompile "org.eclipse.aether:aether-api:${VER_ECLIPSE_AETHER}"
}

task cleanMavenProjectDir(type: Delete) { delete MAVEN_PROJECT_DIR }

task copySourceFiles(type: Copy) {
	from "src/main/java"
	into "${MAVEN_PROJECT_DIR}/src/main/java"
}

// this task is temporary, it should be removed once maven plugin stabilizes
// and can depend on released version of lib and lib-extra
task installAllCompileDependenciesInLocalMavenRepo

project.configurations.compile.resolvedConfiguration.resolvedArtifacts.each {
	def groupId = it.moduleVersion.id.group
	def artifactId = it.moduleVersion.id.name
	def version = it.moduleVersion.id.version
	def file = it.file

	if (artifactId == "lib" || artifactId == "lib-extra") {
		artifactId = "spotless-" + artifactId
		version = SPOTLESS_LIB_VERSION
	}

	def installDependency = task "install_${artifactId}"(type: Exec) {
		commandLine "mvn", "org.apache.maven.plugins:maven-install-plugin:2.3.1:install-file",
				"-Dfile=${file}",
				"-DgroupId=${groupId}",
				"-DartifactId=${artifactId}",
				"-Dversion=${version}",
				"-Dpackaging=jar",
				"-DlocalRepositoryPath=${LOCAL_MAVEN_REPO_DIR}"
	}

	installAllCompileDependenciesInLocalMavenRepo.dependsOn installDependency
}

task createPomXml(dependsOn: installAllCompileDependenciesInLocalMavenRepo) {
	doLast {
		def additionalDependencies = project.configurations.compile.resolvedConfiguration.resolvedArtifacts.findAll {
			def artifactId = it.moduleVersion.id.name
			return artifactId != "lib" && artifactId != "lib-extra"
		}.collect {
			def groupId = it.moduleVersion.id.group
			def artifactId = it.moduleVersion.id.name
			def version = it.moduleVersion.id.version

			return "<dependency>\n" +
					"      <groupId>${groupId}</groupId>\n" +
					"      <artifactId>${artifactId}</artifactId>\n" +
					"      <version>${version}</version>\n" +
					"    </dependency>\n"
		}.join("\n")

		def versions = [
			spotlessMavenPluginVersion: project.versionMaven,
			mavenApiVersion           : VER_MAVEN_API,
			eclipseAetherVersion      : VER_ECLIPSE_AETHER,
			spotlessLibVersion        : SPOTLESS_LIB_VERSION,
			additionalDependencies    : additionalDependencies,
			localMavenRepositoryDir   : LOCAL_MAVEN_REPO_DIR
		]

		def pomXmlTemplate = Paths.get(PROJECT_DIR, "src", "main", "resources", "pom.xml.mustache")
		def newPomXml = Paths.get(MAVEN_PROJECT_DIR, "pom.xml")

		Files.newBufferedReader(pomXmlTemplate).withCloseable { reader ->
			Files.newBufferedWriter(newPomXml, UTF_8, CREATE_NEW, TRUNCATE_EXISTING).withCloseable { writer ->
				def mustache = new DefaultMustacheFactory().compile(reader, "pom")
				mustache.execute(writer, versions)
			}
		}
	}
}

task runMavenBuild(type: Exec, dependsOn: [
	cleanMavenProjectDir,
	copySourceFiles,
	createPomXml
]) {
	workingDir MAVEN_PROJECT_DIR
	commandLine "mvn", "clean", "install"
}

task buildMavenPlugin(type: Copy, dependsOn: runMavenBuild) {
	from "${MAVEN_PROJECT_DIR}/target"
	into "${BUILD_DIR}/libs"
}

task createLocalMavenRepositoryForTests(type: Exec, dependsOn: buildMavenPlugin) {
	workingDir "${BUILD_DIR}/libs"
	commandLine "mvn", "org.apache.maven.plugins:maven-install-plugin:2.3.1:install-file",
			"-Dfile=spotless-maven-plugin-${project.versionMaven}.jar",
			"-DgroupId=com.diffplug.spotless",
			"-DartifactId=spotless-maven-plugin",
			"-Dversion=${project.versionMaven}",
			"-Dpackaging=maven-plugin",
			"-DlocalRepositoryPath=${LOCAL_MAVEN_REPO_DIR}"
}

test {
	testLogging { exceptionFormat = 'full' }
	// pass location of the local maven repository and plugin version to junit tests
	systemProperty "localMavenRepositoryDir", "${LOCAL_MAVEN_REPO_DIR}"
	systemProperty "spotlessMavenPluginVersion", project.versionMaven
}

test.dependsOn createLocalMavenRepositoryForTests
build.dependsOn buildMavenPlugin
